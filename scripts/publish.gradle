allprojects { project ->
    project.plugins.withType(MavenPublishPlugin) {
        project.apply plugin: 'signing'

        signing {
            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications
        }

        publishing {
            repositories {
                maven {
                    name = 'xpdustry'
                    url = 'https://maven.xpdustry.com/anuken-releases/'
                    credentials {
                        username = findProperty("xpdustryUsername")
                        password = findProperty("xpdustryPassword")
                    }
                }
            }
        }
    }

    project.repositories {
        mavenLocal()
    }

    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.github.Anuken.Arc') {
                details.useVersion project.version
            }
        }
    }
}
